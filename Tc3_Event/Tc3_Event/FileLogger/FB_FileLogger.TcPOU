<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_FileLogger" Id="{1f305008-1e9f-45e5-b339-4427cb30dd71}" SpecialFunc="None">
    <Declaration><![CDATA[// Provide logging 
FUNCTION_BLOCK FB_FileLogger IMPLEMENTS I_Logger
VAR_INPUT
	eLogLevel:				E_LogLevel:= E_LogLevel.Verbose;
	sPathName:				STRING(255):= 'C:\temp\logger.txt';
END_VAR
VAR
	fbFile:					FB_File;
	aBuffer:				ARRAY[0..99] OF FB_Message;
    nBuffer:				UINT := 0;                     // Message count
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[// https://peterzerlauth.com/]]></ST>
    </Implementation>
    <Method Name="M_Log" Id="{a4f0a602-53fb-4ee6-8de0-ab6af0f15a67}">
      <Declaration><![CDATA[METHOD PUBLIC M_Log : BOOL
VAR_INPUT
	fbMessage:			FB_Message;
END_VAR
VAR
	nTimestamp: 	ULINT:= F_GetSystemTime();
	nIndex: 		INT;
	sLogLine:		STRING(255);
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Log Level
IF eLogLevel > fbMessage.eLogLevel THEN
	M_Log:= TRUE;
	RETURN;
END_IF

// Remove expired messages (>1 s old)
nIndex := 0;
WHILE nIndex < nBuffer DO
    IF (nTimestamp - aBuffer[nIndex].nTimestamp) > 1_000_000 THEN // 1 s = 1e6 µs
        MEMMOVE(ADR(aBuffer[nIndex]), ADR(aBuffer[nIndex + 1]), SIZEOF(FB_Message) * (nBuffer - nIndex - 1));
        nBuffer := nBuffer - 1;
    ELSE
        nIndex := nIndex + 1;
    END_IF
END_WHILE

// Skip if message already in buffer
FOR nIndex := 0 TO nBuffer - 1 DO
    IF (aBuffer[nIndex].nID = fbMessage.nID)
       AND (aBuffer[nIndex].sDefault = fbMessage.sDefault) THEN
	   aBuffer[nIndex].nTimestamp:= fbMessage.nTimestamp;
        M_Log := TRUE;
        RETURN;
    END_IF
END_FOR

// Add new message
IF nBuffer < 99 THEN
    aBuffer[nBuffer] := fbMessage;
    aBuffer[nBuffer].nTimestamp := nTimestamp;
    nBuffer := nBuffer + 1;
END_IF

// Format log line
sLogLine := CONCAT('[', FILETIME64_TO_ISO8601(nTimestamp, 0, FALSE, 3));
sLogLine := CONCAT(sLogLine, '] ');
sLogLine := CONCAT(sLogLine, TO_STRING(fbMessage.eLogLevel));
sLogLine := CONCAT(sLogLine, ' [');
sLogLine := CONCAT(sLogLine, UDINT_TO_STRING(fbMessage.nID));
sLogLine := CONCAT(sLogLine, '] ');
sLogLine := CONCAT(sLogLine, fbMessage.sDefault);
sLogLine := CONCAT(sLogLine, '$R$N');

// Write once per new message
fbFile.M_Open(sPathName);
fbFile.M_Write(sLogLine);
fbFile.M_Close();

M_Log := TRUE;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="P_LogLevel" Id="{4a4febf7-9f38-4135-9708-e563fad83f87}">
      <Declaration><![CDATA[PROPERTY PUBLIC P_LogLevel : E_LogLevel]]></Declaration>
      <Get Name="Get" Id="{fb1589e6-403d-4ff2-b9ef-b07d913d1a98}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_LogLevel:= eLogLevel;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{5bac8b9c-61cd-4768-8844-c766134a401d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[eLogLevel:= P_LogLevel;
]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>