<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="FB_Event" Id="{fcfa56ff-d0f1-4149-9de7-6f44354915c6}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'reflection'} 
FUNCTION_BLOCK FB_Event IMPLEMENTS I_Event
VAR_INPUT
	iLogger:					I_Logger;
END_VAR
VAR_OUTPUT
	fbArguments:				FB_Argument;
END_VAR
VAR
    {attribute 'hide'} 
    {attribute 'instance-path'} 
    {attribute 'noinit'} 
    sInstancePath:				STRING(255); 
	fbSourceInfo: 				FB_TcSourceInfo;
	bVerbose: 					BOOL;
	bInfo: 						BOOL;
	bWarning: 					BOOL;
	bError: 					BOOL;
	bCritical: 					BOOL;
	fbVerbose: 					FB_TcAlarm;
	fbInfo: 					FB_TcAlarm;
	fbWarning: 					FB_TcAlarm;
	fbError: 					FB_TcAlarm;
	fbCritical: 				FB_TcAlarm;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// all events stay alive until mehtod is not called any more

IF bVerbose THEN
	bVerbose:= FALSE;
ELSIF fbVerbose.bActive THEN
	fbVerbose.Clear();
END_IF

IF bInfo THEN
	bInfo:= FALSE;
ELSIF fbInfo.bActive THEN
	fbInfo.Clear();
END_IF

IF bWarning THEN
	bWarning:= FALSE;
ELSIF fbWarning.bActive THEN
	fbWarning.Clear();
END_IF

IF bError THEN
	bError:= FALSE;
ELSIF fbError.bActive THEN
	fbError.Clear();
END_IF

IF bCritical THEN
	bCritical:= FALSE;
ELSIF fbCritical.bActive THEN
	fbCritical.Clear();
END_IF]]></ST>
    </Implementation>
    <Method Name="FB_Init" Id="{256fe292-0b03-4593-83b5-0da540634dee}">
      <Declaration><![CDATA[//FB_Init ist immer implizit verfügbar und wird primär für die Initialisierung verwendet.
//Der Rückgabewert wird nicht ausgewertet. Für gezielte Einflussnahme können Sie
//die Methoden explizit deklarieren und darin mit dem Standard-Initialisierungscode 
//zusätzlichen Code bereitstellen. Sie können den Rückgabewert auswerten.
METHOD FB_Init: BOOL
VAR_INPUT    
bInitRetains: BOOL; // TRUE: Die Retain-Variablen werden initialisiert (Reset warm / Reset kalt)    
bInCopyCode: BOOL;  // TRUE: Die Instanz wird danach in den Kopiercode kopiert (Online-Change)
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[sInstancePath:= F_InstancePath(sInstancePath);
fbSourceInfo.sName:= sInstancePath;

fbVerbose.CreateEx(TC_Events.Tc3_Event.Verbose, FALSE, fbSourceInfo);
fbInfo.CreateEx(TC_Events.Tc3_Event.Info, FALSE, fbSourceInfo);
fbWarning.CreateEx(TC_Events.Tc3_Event.Warning, FALSE, fbSourceInfo);
fbError.CreateEx(TC_Events.Tc3_Event.Error, FALSE, fbSourceInfo);
fbCritical.CreateEx(TC_Events.Tc3_Event.Critical, FALSE, fbSourceInfo);]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Critical" Id="{7b4c91bf-8e3c-413c-a148-e271617106d9}">
      <Declaration><![CDATA[METHOD PUBLIC M_Critical : BOOL
VAR_INPUT
	nID:				UDINT;
	sMessage:			STRING(255);
END_VAR
VAR
	stMessage:			ST_Message;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stMessage.eLogLevel:= E_LogLevel.Critical;
stMessage.nID:= nID;
stMessage.nTimestamp:= F_GetSystemTime();
stMessage.sMessage:= sMessage;
stMessage.sArguments:= fbArguments.P_Value;
stMessage.sDefault:= F_PrintF(sMessage, fbArguments.P_Value);
stMessage.sSource:= sInstancePath;
fbArguments.M_Clear();

bCritical:= TRUE;
fbCritical.ipArguments.Clear();
fbCritical.ipArguments.AddUDInt(nID);
fbCritical.ipArguments.AddString(stMessage.sDefault);
fbCritical.Raise(stMessage.nTimestamp);

IF iLogger <> 0 THEN
	iLogger.M_Log(stMessage);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Error" Id="{11687e23-ea95-4e4f-aed4-4799ffa1ad84}">
      <Declaration><![CDATA[METHOD PUBLIC M_Error : BOOL
VAR_INPUT
	nID:				UDINT;
	sMessage:			STRING(255);
END_VAR
VAR
	stMessage:			ST_Message;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stMessage.eLogLevel:= E_LogLevel.Error;
stMessage.nID:= nID;
stMessage.nTimestamp:= F_GetSystemTime();
stMessage.sMessage:= sMessage;
stMessage.sArguments:= fbArguments.P_Value;
stMessage.sDefault:= F_PrintF(sMessage, fbArguments.P_Value);
stMessage.sSource:= sInstancePath;
fbArguments.M_Clear();

bError:= TRUE;
fbError.ipArguments.Clear();
fbError.ipArguments.AddUDInt(nID);
fbError.ipArguments.AddString(stMessage.sDefault);
fbError.Raise(stMessage.nTimestamp);

IF iLogger <> 0 THEN
	iLogger.M_Log(stMessage);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Info" Id="{7a920e55-da6d-473b-9aba-119c4de007e0}">
      <Declaration><![CDATA[METHOD PUBLIC M_Info : BOOL
VAR_INPUT
	nID:				UDINT;
	sMessage:			STRING(255);
END_VAR
VAR
	stMessage:			ST_Message;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stMessage.eLogLevel:= E_LogLevel.Info;
stMessage.nID:= nID;
stMessage.nTimestamp:= F_GetSystemTime();
stMessage.sMessage:= sMessage;
stMessage.sArguments:= fbArguments.P_Value;
stMessage.sDefault:= F_PrintF(sMessage, fbArguments.P_Value);
stMessage.sSource:= sInstancePath;
fbArguments.M_Clear();

bInfo:= TRUE;
fbInfo.ipArguments.Clear();
fbInfo.ipArguments.AddUDInt(nID);
fbInfo.ipArguments.AddString(stMessage.sDefault);
fbInfo.Raise(stMessage.nTimestamp);

IF iLogger <> 0 THEN
	iLogger.M_Log(stMessage);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Verbose" Id="{db95e385-16ed-493b-a6a5-28cc6625776b}">
      <Declaration><![CDATA[METHOD PUBLIC M_Verbose : BOOL
VAR_INPUT
	sMessage:			STRING(255);
END_VAR
VAR
	stMessage:			ST_Message;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stMessage.eLogLevel:= E_LogLevel.Verbose;
stMessage.nID:= 0;
stMessage.nTimestamp:= F_GetSystemTime();
stMessage.sMessage:= sMessage;
stMessage.sArguments:= fbArguments.P_Value;
stMessage.sDefault:= F_PrintF(sMessage, fbArguments.P_Value);
stMessage.sSource:= sInstancePath;
fbArguments.M_Clear();

bVerbose:= TRUE;
fbVerbose.ipArguments.Clear();
fbVerbose.ipArguments.AddString(stMessage.sDefault);
fbVerbose.Raise(stMessage.nTimestamp);

IF iLogger <> 0 THEN
	iLogger.M_Log(stMessage);
END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Warning" Id="{1aa0d3ef-0f74-4102-97d7-7d587822db85}">
      <Declaration><![CDATA[METHOD PUBLIC M_Warning : BOOL
VAR_INPUT
	nID:				UDINT;
	sMessage:			STRING(255);
END_VAR
VAR
	stMessage:			ST_Message;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[stMessage.eLogLevel:= E_LogLevel.Warning;
stMessage.nID:= nID;
stMessage.nTimestamp:= F_GetSystemTime();
stMessage.sMessage:= sMessage;
stMessage.sArguments:= fbArguments.P_Value;
stMessage.sDefault:= F_PrintF(sMessage, fbArguments.P_Value);
stMessage.sSource:= sInstancePath;
fbArguments.M_Clear();

bWarning:= TRUE;
fbWarning.ipArguments.Clear();
fbWarning.ipArguments.AddUDInt(nID);
fbWarning.ipArguments.AddString(stMessage.sDefault);
fbWarning.Raise(stMessage.nTimestamp);

IF iLogger <> 0 THEN
	iLogger.M_Log(stMessage);
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="P_Argument" Id="{dd3bce54-33d7-4a6c-8d06-ad6a5d0ec566}">
      <Declaration><![CDATA[PROPERTY PUBLIC P_Argument : I_Argument]]></Declaration>
      <Get Name="Get" Id="{9148b4be-b1fa-4fe4-a389-e96c939bba14}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_Argument:= fbArguments;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="P_Logger" Id="{98c079df-4944-4aae-a3d6-ca12ac41d1d4}">
      <Declaration><![CDATA[PROPERTY PUBLIC P_Logger : I_Logger]]></Declaration>
      <Get Name="Get" Id="{7f49254e-94b6-4972-a1b5-02a92d10e780}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[P_Logger:= iLogger;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{4bbb606c-4053-49cc-ae04-84e1809ace25}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[iLogger:= P_Logger;]]></ST>
        </Implementation>
      </Set>
    </Property>
  </POU>
</TcPlcObject>