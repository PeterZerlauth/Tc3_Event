<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="F_Log" Id="{3d8b8d8d-2aeb-485f-a382-76004536aaec}" SpecialFunc="None">
    <Declaration><![CDATA[// Standalone logger, if no logger is attached to FB_Event, nice for tessting
FUNCTION F_Log : BOOL
VAR_INPUT
	fbMessage:			REFERENCE TO FB_Message;
END_VAR
VAR
	sMessage:			STRING(255):= 'Logger = 0 | ';
    nIndex:				UINT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[
// --- Build log line
sMessage := CONCAT('Logger null | ', fbMessage.sSource);
sMessage := CONCAT(sMessage, ': ');
sMessage := CONCAT(sMessage, fbMessage.sDefault);

// --- Keep active
nIndex := 0;
WHILE nIndex < GVL.nBuffer DO
    IF GVL.aBuffer[nIndex].sDefault = fbMessage.sDefault THEN
		GVL.aBuffer[nIndex].bActive:= TRUE;
        F_Log := TRUE;
        RETURN;
    END_IF
    nIndex := nIndex + 1;
END_WHILE

// --- Add to buffer ---
IF GVL.nBuffer < 99 THEN
    GVL.aBuffer[GVL.nBuffer] := fbMessage;
    GVL.aBuffer[GVL.nBuffer].nTimestamp := GVL.nTimestamp;
    GVL.nBuffer := GVL.nBuffer + 1;
END_IF

// --- Log message ---
CASE fbMessage.eLogLevel OF
    E_LogLevel.Verbose, E_LogLevel.Info:
        ADSLOGSTR(ADSLOG_MSGTYPE_HINT, '%s', sMessage);
    E_LogLevel.Warning:
        ADSLOGSTR(ADSLOG_MSGTYPE_WARN, '%s', sMessage);
    E_LogLevel.Error, E_LogLevel.Critical:
        ADSLOGSTR(ADSLOG_MSGTYPE_ERROR, '%s', sMessage);
END_CASE

// --- Remove outdated messages (>1 s old) ---
nIndex := 0;
WHILE nIndex < GVL.nBuffer DO
    IF (GVL.nTimestamp - GVL.aBuffer[nIndex].nTimestamp) > 1_000_000_000 THEN // 1 s = 1e9 ns
        MEMMOVE(ADR(GVL.aBuffer[nIndex]), ADR(GVL.aBuffer[nIndex + 1]), SIZEOF(FB_Message) * (GVL.nBuffer - nIndex - 1));
        GVL.nBuffer := GVL.nBuffer - 1;
    ELSE
        nIndex := nIndex + 1;
    END_IF
END_WHILE


F_Log := TRUE;
]]></ST>
    </Implementation>
  </POU>
</TcPlcObject>